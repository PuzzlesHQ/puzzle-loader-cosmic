import dev.puzzleshq.buildsrc.DepUtils
import org.hjson.JsonArray
import org.hjson.JsonObject
import org.hjson.Stringify

plugins {
    id 'java'
    id 'maven-publish'
    id("io.github.sgtsilvio.gradle.maven-central-publishing") version "0.4.0"
    id 'com.gradleup.shadow' version '8.3.6'
}

def ref = System.getenv("GITHUB_REF")

group = p_group
version = (ref != null && ref.startsWith("refs/tags/")) ? ref.tokenize('/').last().replaceFirst(/^v/, '') : p_version

String mavenUrl = System.getenv("MAVEN_URL")

String envRepo = System.getenv("MAVEN_REPO");
String mavenRepo = envRepo != null ? envRepo : "releases";

java {
    sourceCompatibility = '17'
    targetCompatibility = '17'
}

repositories {
    ivy {
        name "Cosmic Reach"
        url "https://github.com/PuzzlesHQ/CRArchive/releases/download"
        patternLayout {
            artifact "/[revision]/cosmic-reach-[classifier]-[revision].jar"
        }
        // This is required in Gradle 6.0+ as metadata file (ivy.xml) is mandatory
        metadataSources {
            artifact()
        }

        content {
            it.includeModule "finalforeach", "cosmic-reach"
        }
    }
    mavenLocal()
    mavenCentral()
    maven {
        name = 'Jitpack'
        url 'https://jitpack.io' 
    }
    maven {
        name = 'Google'
        url "https://maven.google.com/" 
    }
    maven {
        name = 'Minecraft'
        url "https://libraries.minecraft.net" 
    }
    maven {
        name = 'MavenCentral'
        url "https://repo1.maven.org/maven2/" 
    }
    maven {
        name = 'MixinSquared'
        url "https://maven.bawnorton.com/releases" 
    }
    maven {
        name "neoforged"
        url "https://maven.neoforged.net/release"
    }
}

sourceSets {
    client
    common
    server
}

configurations {
    allCompileOnly

    clientCompileOnly.extendsFrom(allCompileOnly)
    commonCompileOnly.extendsFrom(allCompileOnly)
    serverCompileOnly.extendsFrom(allCompileOnly)

    allImpl
    clientImplementation.extendsFrom(allImpl)
    commonImplementation.extendsFrom(allImpl)
    serverImplementation.extendsFrom(allImpl)

    allImplLoader
    clientImplementation.extendsFrom(allImplLoader)
    commonImplementation.extendsFrom(allImplLoader)
    serverImplementation.extendsFrom(allImplLoader)

    clientBundle
    commonBundle
    serverBundle

    clientImplementation.extendsFrom(clientBundle)
    commonImplementation.extendsFrom(commonBundle)
    serverImplementation.extendsFrom(serverBundle)

    clientInternal
    serverInternal

    clientImplementation.extendsFrom(clientInternal)
    serverImplementation.extendsFrom(serverInternal)
    serverImplementation.extendsFrom(commonBundle)

}

boolean jitpackPuzzle = false;

String jpVersion = "79a4fd9a75"
//String centralVersion = "0.0.0-alpha"
String centralVersion = "1.3.1-alpha"

final String crVersion = "0.5.7-alpha"

var jar = files("./lib/Cosmic-Reach-0.4.17b.jar")
boolean usingSingleJar = false

final HashMap<String, String>[] excludes = new HashMap<String, String>[]{
        ["group": "org.slf4j", "module": "slf4j-api"] as HashMap,
        ["group": "com.google.code.gson", "module": "gson"] as HashMap,
        ["group": "org.apache.logging.log4j", "module": "log4j-core"] as HashMap,
        ["group": "org.apache.logging.log4j", "module": "log4j-api"] as HashMap,
        ["group": "org.javassist", "module": "javassist"] as HashMap,
        ["group": "com.google.errorprone", "module": "error_prone_annotations"] as HashMap,
        ["group": "com.google.guava", "module": "guava"] as HashMap,
        ["group": "com.google.guava", "module": "failureaccess"] as HashMap,
        ["group": "com.google.guava", "module": "listenablefuture"] as HashMap,
        ["group": "com.google.j2objc", "module": "j2objc-annotations"] as HashMap,
        ["group": "org.jspecify", "module": "jspecify"] as HashMap,
        ["group": "org.apache.commons", "module": "commons-compress"] as HashMap,
        ["group": "org.apache.commons", "module": "commons-lang3"] as HashMap,
        ["group": "commons-io", "module": "commons-io"] as HashMap,
        ["group": "commons-codec", "module": "commons-codec"] as HashMap,
        ["group": "com.google.code.findbugs", "module": "jsr305"] as HashMap,
        ["group": "org.ow2.asm", "module": "asm"] as HashMap,
        ["group": "org.ow2.asm", "module": "asm-tree"] as HashMap,
        ["group": "org.ow2.asm", "module": "asm-commons"] as HashMap,
        ["group": "org.ow2.asm", "module": "asm-analysis"] as HashMap,
        ["group": "org.ow2.asm", "module": "asm-util"] as HashMap,
        ["group": "net.jodah", "module": "typetools"] as HashMap,
}

dependencies {

    clientCompileOnly "org.jetbrains:annotations:24.0.0"
    serverCompileOnly "org.jetbrains:annotations:24.0.0"
    commonCompileOnly "org.jetbrains:annotations:24.0.0"

    allImpl("net.neoforged:bus:$eventbus_version") { for (HashMap<String, String> map : excludes) exclude map }
    allImpl("net.jodah:typetools:$typetools_version") { for (HashMap<String, String> map : excludes) exclude map }

    if (jitpackPuzzle) {
        clientBundle "com.github.PuzzlesHQ:puzzle-loader-core:$jpVersion:client"
        serverBundle "com.github.PuzzlesHQ:puzzle-loader-core:$jpVersion:server"
        allImpl("com.github.PuzzlesHQ:puzzle-loader-core:$jpVersion:common")
    } else {
        clientBundle "dev.puzzleshq:puzzle-loader-core:$centralVersion:client"
        serverBundle "dev.puzzleshq:puzzle-loader-core:$centralVersion:server"
        allImpl("dev.puzzleshq:puzzle-loader-core:$centralVersion:common")
    }

    if (usingSingleJar) {
        clientImplementation(jar)
        commonImplementation(jar)
        serverImplementation(jar)
    } else {
        clientImplementation("finalforeach:cosmic-reach:$crVersion:client")
        commonImplementation("finalforeach:cosmic-reach:$crVersion:server")
        serverImplementation("finalforeach:cosmic-reach:$crVersion:server")
    }

    clientImplementation(sourceSets.common.output)
    serverImplementation(sourceSets.common.output)

    allImplLoader ("io.sigpipe:jbsdiff:$jbsdiff_version") { for (HashMap<String, String> map : excludes) exclude map }

    allImplLoader("net.fabricmc:sponge-mixin:$mixin_version") { for (HashMap<String, String> map : excludes) exclude map }
    allImplLoader annotationProcessor("io.github.llamalad7:mixinextras-common:$mixin_extras_version") { for (HashMap<String, String> map : excludes) exclude map }
    allImplLoader(annotationProcessor("com.github.bawnorton.mixinsquared:mixinsquared-common:$mixin_squared_version")) { for (HashMap<String, String> map : excludes) exclude map }
    allImplLoader("com.github.PuzzlesHQ:mixinconstraints-java8:$mixin_constraints_version") { for (HashMap<String, String> map : excludes) exclude map }

    allImplLoader ("net.sf.jopt-simple:jopt-simple:$jopt_simple_version") { for (HashMap<String, String> map : excludes) exclude map }
    allImplLoader ("com.google.guava:guava:$google_guava_version") { for (HashMap<String, String> map : excludes) exclude map }
    allImplLoader ("com.google.code.gson:gson:$gson_version") { for (HashMap<String, String> map : excludes) exclude map }
    allImplLoader ("org.javassist:javassist:$javassist_version") { for (HashMap<String, String> map : excludes) exclude map }

    allImplLoader ("com.github.villadora:semver:$semver_version") { for (HashMap<String, String> map : excludes) exclude map }

    allImplLoader ("org.apache.logging.log4j:log4j-api:$log4j_version") { for (HashMap<String, String> map : excludes) exclude map }
    allImplLoader ("org.apache.logging.log4j:log4j-core:$log4j_version") { for (HashMap<String, String> map : excludes) exclude map }
    allImplLoader ("org.reflections:reflections:$reflections_version") { for (HashMap<String, String> map : excludes) exclude map }

    allImplLoader ("org.ow2.asm:asm:$asm_version") { for (HashMap<String, String> map : excludes) exclude map }
    allImplLoader ("org.ow2.asm:asm-tree:$asm_version") { for (HashMap<String, String> map : excludes) exclude map }
    allImplLoader ("org.ow2.asm:asm-util:$asm_version") { for (HashMap<String, String> map : excludes) exclude map }
    allImplLoader ("org.ow2.asm:asm-analysis:$asm_version") { for (HashMap<String, String> map : excludes) exclude map }
    allImplLoader ("org.ow2.asm:asm-commons:$asm_version") { for (HashMap<String, String> map : excludes) exclude map }

    allImplLoader ("dev.puzzleshq:puzzle-annotations:$puzzle_annotations_version") { for (HashMap<String, String> map : excludes) exclude map }
    allImplLoader ("dev.puzzleshq:access-writer:$puzzle_access_writer_version" ) { for (HashMap<String, String> map : excludes) exclude map }
    allImplLoader ("dev.puzzleshq:mod-format-framework:$puzzle_mod_format_framework_version") { for (HashMap<String, String> map : excludes) exclude map }

    allImplLoader ("org.jspecify:jspecify:$jspecify_version") { for (HashMap<String, String> map : excludes) exclude map }
    allImplLoader ("com.google.errorprone:error_prone_annotations:$google_error_prone_annotations_version") { for (HashMap<String, String> map : excludes) exclude map }
    allImplLoader ("com.google.guava:failureaccess:$google_failureaccess_version") { for (HashMap<String, String> map : excludes) exclude map }
    allImplLoader ("com.google.guava:listenablefuture:$google_listenablefuture_version") { for (HashMap<String, String> map : excludes) exclude map }
    allImplLoader ("com.google.j2objc:j2objc-annotations:$google_j2objc_annotations_version") { for (HashMap<String, String> map : excludes) exclude map }
    allImplLoader ("com.google.code.findbugs:jsr305:$google_findbugs_jsr305_version") { for (HashMap<String, String> map : excludes) exclude map }

    allImplLoader ("org.apache.commons:commons-compress:$apache_commons_compress_version") { for (HashMap<String, String> map : excludes) exclude map }
    allImplLoader ("org.apache.commons:commons-lang3:$apache_commons_lang3_version") { for (HashMap<String, String> map : excludes) exclude map }
    allImplLoader ("commons-codec:commons-codec:$apache_commons_codec_version") { for (HashMap<String, String> map : excludes) exclude map }
    allImplLoader ("commons-io:commons-io:$apache_commons_io_version") { for (HashMap<String, String> map : excludes) exclude map }

    allImplLoader ("org.hjson:hjson:$hjson_version") {for (HashMap<String, String> map : excludes) exclude map }
}

var runDir = file("./run")

tasks.register("runClient", JavaExec) {
    classpath += sourceSets.client.runtimeClasspath
    classpath += sourceSets.common.runtimeClasspath

    args("--save-location=\"" + runDir.getAbsolutePath() + "\"");
//    jvmArgs += '--add-opens=java.base/java.lang=ALL-UNNAMED'
    mainClass = "dev.puzzleshq.puzzleloader.loader.launch.pieces.ClientPiece"
    if (!runDir.exists()) runDir.mkdirs()
    workingDir(runDir)
}

tasks.register("runVanillaClient", JavaExec) {
    classpath += sourceSets.client.runtimeClasspath
    classpath += sourceSets.common.runtimeClasspath

    mainClass = "finalforeach.cosmicreach.lwjgl3.Lwjgl3Launcher"
    if (!runDir.exists()) runDir.mkdirs()
    workingDir(runDir)
}

tasks.register("runServer", JavaExec) {
    classpath += sourceSets.server.runtimeClasspath
    classpath += sourceSets.common.runtimeClasspath

    args("--save-location=\"" + runDir.getAbsolutePath() + "\"");

    mainClass = "dev.puzzleshq.puzzleloader.loader.launch.pieces.ServerPiece"
    if (!runDir.exists()) runDir.mkdirs()
    workingDir(runDir)
}

tasks.register("runVanillaServer", JavaExec) {
    classpath += sourceSets.server.runtimeClasspath
    classpath += sourceSets.common.runtimeClasspath

    mainClass = "finalforeach.cosmicreach.server.ServerLauncher"
    if (!runDir.exists()) runDir.mkdirs()
    workingDir(runDir)
}

java {
    withJavadocJar()
    withSourcesJar()

    sourceCompatibility = '17'
    targetCompatibility = '17'
}

tasks.register("buildServerJar", com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
    mergeServiceFiles()

    archiveClassifier = "server"
    from sourceSets.server.output
}

tasks.register("buildServerSourcesJar", Jar) {
    from sourceSets.server.allSource
    archiveClassifier = "server-sources"
}

tasks.register("buildClientJar", com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
    mergeServiceFiles()

    archiveClassifier = "client"
    from sourceSets.client.output
}

tasks.register("buildClientSourcesJar", Jar) {
    from sourceSets.client.allSource
    archiveClassifier = "client-sources"
}

tasks.register("buildCommonJar", com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
    mergeServiceFiles()

    archiveClassifier = "common"
    from sourceSets.common.output
}

tasks.register("buildCommonSourcesJar", Jar) {
    from sourceSets.common.allSource
    archiveClassifier = "common-sources"
}

processCommonResources {
    def projectProperties = [
            "p_name": p_name,
            "p_version": ((String)version).contains(".") ? version : "69.69.69",
            "p_desc": p_desc,

            "p_id": p_id,
            "p_group": p_group,

            "p_git": p_git
    ]

    inputs.properties projectProperties

    filesMatching(["puzzle_project.json", "puzzle.mod.json"]) {
        expand projectProperties
    }
}

publishing {
    repositories {
        maven {
            name = "PuzzleHQsMaven"
            url = mavenUrl+ "/" +mavenRepo
            credentials {
                username = System.getenv("MAVEN_NAME")
                password = System.getenv("MAVEN_SECRET")
            }
            authentication {
                basic(BasicAuthentication)
            }
        }
    }

    publications {
        maven(MavenPublication) {
            groupId = p_group
            artifactId = p_id

            from components.java

            artifact source: buildClientJar, classifier: 'client', extension: 'jar'
            artifact source: buildClientSourcesJar, classifier: 'client-sources', extension: 'jar'

            artifact source: buildServerJar, classifier: 'server', extension: 'jar'
            artifact source: buildServerSourcesJar, classifier: 'server-sources', extension: 'jar'

            artifact source: buildCommonJar, classifier: 'common', extension: 'jar'
            artifact source: buildCommonSourcesJar, classifier: 'common-sources', extension: 'jar'

            pom {
                name = p_name
                description = p_desc
                url = p_git

                licenses {
                    license {
                        name = "GNU Lesser General Public License"
                        url = "https://www.gnu.org/licenses/lgpl-3.0.html"
                        distribution = "LGPL"
                        comments = "This project includes LGPL-licensed components."
                    }
                }

                developers {
                    developer {
                        id = 'mrzombii'
                        name = 'Mr Zombii'
                        email = 'thezombiigames@gmail.com'
                    }
                    developer {
                        id = 'crabking'
                        name = 'CrabKing'
                    }
                }
                scm {
                    connection = "${p_git}.git"
                    developerConnection = "${p_git}.git"
                    url = "${p_git}.git"
                }
            }
        }
    }
}

if (ref != null) {
    signing {
        useGpgCmd()
        sign(publishing.publications[0])
    }
}

wrapper {
    gradleVersion = "8.14"
    distributionType = Wrapper.DistributionType.ALL
}

static def add(String side, JsonObject o, Configuration configuration, String type) {
    JsonArray array
    if (o.get(side) == null) array = new JsonArray()
    else array = o.get(side).asArray()

    for (final def dep in configuration.allDependencies) {
        JsonObject object = new JsonObject();

        def classifier = DepUtils.getClassifier(dep);
        if (classifier != null) object.add("classifier", classifier);
        object.add("groupId", dep.group);
        object.add("artifactId", dep.name);
        object.add("version", dep.version);
        if (dep.name == "puzzle-loader-core")
            object.add("type", "compileOnly");
        else
            object.add("type", type);
        array.add(object)
    }
    o.add(side, array)
}

tasks.register("mkDeps") {
    File file = new File("$projectDir/dependencies.json")

    JsonObject object = new JsonObject();
    add("client", object, configurations.clientBundle, "implementation")
    add("server", object, configurations.serverBundle, "implementation")
    add("common", object, configurations.allImpl, "implementation")

    JsonArray repos = new JsonArray()
    for (i in 0..<project.repositories.size()) {
        def dependency = project.repositories[i];
        if (dependency instanceof MavenArtifactRepository) {
            if (dependency.name == "MavenLocal") continue
            JsonObject repo = new JsonObject()
            repo.add("name", dependency.name)
            repo.add("url", dependency.url.toString())
            repos.add(repo)
        }
    }
    object.add("repos", repos)

    file.write(object.toString(Stringify.FORMATTED))
}

wrapper {
    gradleVersion = "8.14"
    distributionType = Wrapper.DistributionType.ALL
}
