import dev.puzzleshq.buildsrc.WidenerPlugin

plugins {
    id 'java'
    id 'maven-publish'
    id("io.github.sgtsilvio.gradle.maven-central-publishing") version "0.4.0"
    id 'com.gradleup.shadow' version '8.3.6'
}

def ref = System.getenv("GITHUB_REF")

group = p_group
version = (ref != null && ref.startsWith("refs/tags/")) ? ref.tokenize('/').last().replaceFirst(/^v/, '') : p_version

String mavenUrl = System.getenv("MAVEN_URL")

String envRepo = System.getenv("MAVEN_REPO");
String mavenRepo = envRepo != null ? envRepo : "releases";


apply plugin: WidenerPlugin

widenerPlugin {
    widenerPath.set(file("$projectDir/src/common/resources/puzzle-loader-cosmic.manipulator"))
}

repositories {
    ivy {
        name "Cosmic Reach"
        url "https://github.com/PuzzlesHQ/CRArchive/releases/download"
        patternLayout {
            artifact "/[revision]/cosmic-reach-[classifier]-[revision].jar"
        }
        // This is required in Gradle 6.0+ as metadata file (ivy.xml) is mandatory
        metadataSources {
            artifact()
        }

        content {
            it.includeModule "finalforeach", "cosmic-reach"
        }
    }
    mavenCentral()
    mavenLocal()
    maven { url 'https://jitpack.io' }
}

sourceSets {
    client
    common
    server
}

configurations {
    allCompileOnly

    clientCompileOnly.extendsFrom(allCompileOnly)
    commonCompileOnly.extendsFrom(allCompileOnly)
    serverCompileOnly.extendsFrom(allCompileOnly)

    allImpl
    clientImplementation.extendsFrom(allImpl)
    commonImplementation.extendsFrom(allImpl)
    serverImplementation.extendsFrom(allImpl)

    clientBundle
    commonBundle
    serverBundle

    clientImplementation.extendsFrom(clientBundle)
    commonImplementation.extendsFrom(commonBundle)
    serverImplementation.extendsFrom(serverBundle)

    clientInternal
    serverInternal

    clientImplementation.extendsFrom(clientInternal)
    serverImplementation.extendsFrom(serverInternal)
    serverImplementation.extendsFrom(commonBundle)

}

boolean usingSingleJar = false;
String version = "0.4.14-alpha"

var jar = files("./lib/Cosmic-Reach-0.4.10b.jar");

dependencies {

    clientCompileOnly "org.jetbrains:annotations:24.0.0"
    serverCompileOnly "org.jetbrains:annotations:24.0.0"
    commonCompileOnly "org.jetbrains:annotations:24.0.0"

    clientBundle "com.github.PuzzlesHQ:puzzle-loader-core:1.0.3-alpha:client"
    allImpl("com.github.PuzzlesHQ:puzzle-loader-core:1.0.3-alpha:common")

    if (usingSingleJar) {
        clientImplementation(jar)
        commonImplementation(jar)
        serverImplementation(jar)
    } else {
        clientImplementation("finalforeach:cosmic-reach:$version:client")
        commonImplementation("finalforeach:cosmic-reach:$version:server")
        serverImplementation("finalforeach:cosmic-reach:$version:server")
    }

    clientImplementation(sourceSets.common.output)
    serverImplementation(sourceSets.common.output)

    allImpl "org.ow2.asm:asm:$asm_version"
    allImpl "org.ow2.asm:asm-tree:$asm_version"
    allImpl "org.ow2.asm:asm-util:$asm_version"
    allImpl "org.ow2.asm:asm-analysis:$asm_version"
    allImpl "org.ow2.asm:asm-commons:$asm_version"

    allImpl "dev.puzzleshq:puzzle-annotations:1.0.0"
    allImpl "dev.puzzleshq:access-writer:1.0.4+c"
    allImpl "dev.puzzleshq:mod-format-framework:1.0.0"

    allImpl "org.hjson:hjson:$hjson_version"

    allImpl "org.reflections:reflections:$reflections_version"
    allImpl("net.neoforged:bus:$eventbus_version") {
        exclude group: "org.apache.logging.log4j", module: "log4j-api"
    }

    allImpl annotationProcessor("io.github.llamalad7:mixinextras-fabric:$mixin_extras_version")
    allImpl("net.fabricmc:sponge-mixin:$mixin_version") {
        exclude group: "com.google.code.gson", module: "gson"
        exclude group: "com.google.guava", module: "guava"
    }
    allImpl "net.sf.jopt-simple:jopt-simple:$jopt_simple_version"

    allImpl "com.github.zafarkhaja:java-semver:0.10.2"

    allImpl 'org.slf4j:slf4j-ext:1.7.23'
    allImpl 'org.slf4j:slf4j-api:1.7.23'
    allImpl "org.apache.logging.log4j:log4j-api:$slf4j_version"
    allImpl "org.apache.logging.log4j:log4j-core:$slf4j_version"
    allImpl "org.apache.logging.log4j:log4j-slf4j-impl:$slf4j_version"
}

var runDir = file("./run")

tasks.register("runClient", JavaExec) {
    classpath += sourceSets.client.runtimeClasspath
    classpath += sourceSets.common.runtimeClasspath

    mainClass = "dev.puzzleshq.puzzleloader.loader.launch.pieces.ClientPiece"
}

tasks.register("runServer", JavaExec) {
    classpath += sourceSets.server.runtimeClasspath
    classpath += sourceSets.common.runtimeClasspath

    mainClass = "dev.puzzleshq.puzzleloader.loader.launch.pieces.ServerPiece"
    if (!runDir.exists()) runDir.mkdirs()
    workingDir(runDir)
}

java {
    withJavadocJar()
    withSourcesJar()

    sourceCompatibility = '17'
    targetCompatibility = '17'
}

tasks.register("buildServerJar", com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
    mergeServiceFiles()

    archiveClassifier = "server"
    from sourceSets.server.output
    from sourceSets.common.output
}

tasks.register("buildServerSourcesJar", Jar) {
    from sourceSets.server.allSource
    from sourceSets.common.allSource
    archiveClassifier = "server-sources"
}


tasks.register("buildClientJar", com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
    mergeServiceFiles()

    archiveClassifier = "client"
    from sourceSets.client.output
    from sourceSets.common.output
}

tasks.register("buildClientSourcesJar", Jar) {
    from sourceSets.client.allSource
    from sourceSets.common.allSource
    archiveClassifier = "client-sources"
}


processResources {
    def projectProperties = [
            "p_name": p_name,
            "p_version": ((String)version).contains(".") ? version : "69.69.69",
            "p_desc": p_desc,

            "p_id": p_id,
            "p_group": p_group,

            "p_git": p_git
    ]

    inputs.properties projectProperties

    filesMatching(["puzzle_project.json"]) {
        expand projectProperties
    }
}

publishing {
    repositories {
        maven {
            name = "PuzzleHQsMaven"
            url = mavenUrl+ "/" +mavenRepo
            credentials {
                username = System.getenv("MAVEN_NAME")
                password = System.getenv("MAVEN_SECRET")
            }
            authentication {
                basic(BasicAuthentication)
            }
        }
    }

    publications {
        maven(MavenPublication) {
            groupId = p_group
            artifactId = p_id
            version = this.version

            from components.java

            artifact source: buildClientJar, classifier: 'client', extension: 'jar'
            artifact source: buildClientSourcesJar, classifier: 'client-sources', extension: 'jar'

            artifact source: buildServerJar, classifier: 'server', extension: 'jar'
            artifact source: buildServerSourcesJar, classifier: 'server-sources', extension: 'jar'

            pom {
                name = p_name
                description = p_desc
                url = p_git

                licenses {
                    license {
                        name = "GNU Lesser General Public License"
                        url = "https://www.gnu.org/licenses/lgpl-3.0.html"
                        distribution = "LGPL"
                        comments = "This project includes LGPL-licensed components."
                    }
                }

                developers {
                    developer {
                        id = 'mrzombii'
                        name = 'Mr Zombii'
                        email = 'thezombiigames@gmail.com'
                    }
                    developer {
                        id = 'crabking'
                        name = 'CrabKing'
                    }
                }
                scm {
                    connection = "${p_git}.git"
                    developerConnection = "${p_git}.git"
                    url = "${p_git}.git"
                }
            }
        }
    }
}

if (ref != null) {
    signing {
        useGpgCmd()
        sign(publishing.publications[0])
    }
}

wrapper {
    gradleVersion = "8.14"
    distributionType = Wrapper.DistributionType.ALL
}
